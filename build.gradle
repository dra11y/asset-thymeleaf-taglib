apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    ext {
        assetPipelineVersion = project.assetPipelineVersion
        thymeleafVersion = project.thymeleafVersion
    }
    repositories {
        mavenCentral()
    }
}

group 'ch.itds.taglib'
version project.version

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.thymeleaf:thymeleaf:${thymeleafVersion}"
    implementation "com.bertramlabs.plugins:asset-pipeline-core:${assetPipelineVersion}"
    implementation "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'asset-pipeline-thymeleaf-taglib'
                description 'Thymeleaf taglib to simplify the use of the asset-pipeline'
                packaging 'jar'
                url 'https://github.com/itds-ch/asset-pipeline-thymeleaf-taglib'

                scm {
                    connection 'scm:git:https://github.com/itds-ch/asset-pipeline-thymeleaf-taglib.git'
                    developerConnection 'scm:git:https://github.com/itds-ch/asset-pipeline-thymeleaf-taglib.git'
                    url 'https://github.com/itds-ch/asset-pipeline-thymeleaf-taglib'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'm.kobel'
                        name 'Moritz Kobel'
                        email 'moritz.kobel@itds.ch'
                    }
                }
            }
        }
    }
}

